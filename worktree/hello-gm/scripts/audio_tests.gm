
// audio_tests.gm

Log("audio_tests.gm: Initializing...");

global MakeAudioTest = function()
{
    local Main = {
    };

    Main.Init = function()
    {
        .update_id = this:thread(.Update);

		.cam2d = Cam2d();
		.cam2d.InitScreenSpaceSize( Window.GetDimen() );
		.cam2d.SetNearFar(v2(-1000.0f, 1000.0f ));

        .audio = GMAudioStream("audio", g_ip, g_port);
        .fft_magnify_scale = 1.0f;
        .fft_magnify_power = 2.0f;
        .use_microphone = true;
        .use_sine_wave = false;
        .sine_frequency = 5;
        .sine_amplitude = 0.5f;

        .tuner_mode = true;
        .tuner_threshold = 0.1f;
        .piano_playback_test = false;
        .show_beat_estimate = false;
        .beat_estimate_bin = 1;
        .beat_estimate_threshold = 0.01f;
        .beats_per_second_history = table();
        .beats_per_second_index = 0;

        .drawbars = {
            false, // raw
            true,  // fft
            true,  // average
            true,  // difference
        };

        .drawsynthesizer = false;

        .melody_bpm = 120;
        .melody_measures = 4;
        .melody_beats = 2;

		.piano_soundbank = MakeSoundBank(g_resourcePathPrefix + "scripts/PianoSoundBank.gm");

        if ( !g_norobot )
        {
            .audio.SetActive(true);
        }

        .audio.Update();
    };

    Main.Release = function()
    {
        threadKill(.update_id);
    };

    Main.ClearScreen = function()
    {
		Gfx.ClearColor( Gfx.BLUE * v3(0.1f), 1.0f );
		Gfx.ClearDepth(1.0f);
		Gfx.Clear( Gfx.COLOR_BUFFER_BIT | Gfx.DEPTH_BUFFER_BIT );
    };

    Main.Gui = function()
    {
        Gui.Begin("Audio", g_core.screenDimen.x.Int() - 310, g_core.screenDimen.y.Int() - 5);

        if (Gui.Button("Reset Timers"))
        {
            .audio.ResetTimers();
        }

        Gui.Separator();

        .drawbars[0] = Gui.CheckBox("Draw Bars (Raw)", .drawbars[0]);
        .drawbars[1] = Gui.CheckBox("Draw Bars (FFT)", .drawbars[1]);
        .drawbars[2] = Gui.CheckBox("Draw Bars (Average)", .drawbars[2]);
        .drawbars[3] = Gui.CheckBox("Draw Bars (Difference)", .drawbars[3]);

        Gui.Separator();

        .drawsynthesizer = Gui.CheckBox("Draw Synthesizer", .drawsynthesizer);

        .melody_bpm = Gui.SliderInt("Melody BPM", .melody_bpm, 60, 180);
        .melody_measures = Gui.SliderInt("Melody Measures", .melody_measures, 1, 16);
        .melody_beats = Gui.SliderInt("Melody Beats", .melody_beats, 1, 16);

        if (Gui.Button("Play Melody (1)")) { .PlayMelody(.PlayMelody1); }
        if (Gui.Button("Play Melody (2)")) { .PlayMelody(.PlayMelody2); }

        Gui.Separator();

        .fft_magnify_scale = Gui.SliderFloat("FFT Magnify Scale", .fft_magnify_scale, 0.001f, 10.0f);
        .fft_magnify_power = Gui.SliderFloat("FFT Magnify Power", .fft_magnify_power, 0.1f, 3.0f);
        .use_microphone = Gui.CheckBox("Use Microphone", .use_microphone);
        .use_sine_wave = Gui.CheckBox("Use Sine Wave", .use_sine_wave);
        .sine_frequency = Gui.SliderInt("Sine Frequency", .sine_frequency, 1, .audio.GetFFTWindowSize() / 2 - 1);
        .sine_amplitude = Gui.SliderFloat("Sine Amplitude", .sine_amplitude, 0.0f, 2.0f);
        .tuner_mode = Gui.CheckBox("Enable Tuner", .tuner_mode);
        .tuner_threshold = Gui.SliderFloat("Tuner Threshold", .tuner_threshold, 0.000001f, 1.0f);
        .piano_playback_test = Gui.CheckBox("Piano Playback Test", .piano_playback_test);
        .show_beat_estimate = Gui.CheckBox("Beat Estimate", .show_beat_estimate);
        .beat_estimate_bin = Gui.SliderInt("Beat Estimate Bin", .beat_estimate_bin, 1, .audio.GetFFTWindowSize() / 2 - 1);
        .beat_estimate_threshold = Gui.SliderFloat("Beat Estimate Threshold", .beat_estimate_threshold, 0.001f, 0.1f);

        Gui.End();
    };

    Main.DrawAudio = function()
    {
		Gfx.Viewport( v2(0.0f), g_core.screenDimen );
		Gfx.BeginDefaultShader();

        .cam2d.Begin();

		Gfx.Color(Gfx.WHITE, 1.0f);

        if ( .drawbars[0] )
        {
            .audio.DrawFrameRawBars(0, Gfx.BLUE, 1.0f);
        }
        else
        {
            .audio.DrawFrameRawWaveform(0, Gfx.BLUE, 1.0f);
        }

        if (.drawbars[1])
        {
            .audio.DrawFrameFFTBars(0, Gfx.YELLOW, 0.5f);
        }
        else
        {
            .audio.DrawFrameFFTWaveform(0, Gfx.YELLOW, 0.5f);
        }

        if (.drawbars[2])
        {
            .audio.DrawFrameAverageBars(Gfx.WHITE, 0.3f);
        }
        else
        {
            .audio.DrawFrameAverageWaveform(Gfx.WHITE, 0.3f);
        }

        if (.drawbars[3])
        {
            .audio.DrawFrameDifferenceBars(Gfx.RED, 1.0f);
        }
        else
        {
            .audio.DrawFrameDifferenceWaveform(Gfx.RED, 1.0f);
        }

        if (.drawsynthesizer)
        {
            .audio.DrawSynthesizer(v2(1024.0f, 512.0f), Gfx.GREEN, 1.0f);
        }

        .cam2d.End();

		Gfx.EndDefaultShader();
		Gfx.Viewport( v2(0), g_core.screenDimen );
    };

    Main.Update = function()
    {
        while (true)
        {
    		RegisterDraw( DrawLayers.Clear, .ClearScreen, this );
    		RegisterDraw( DrawLayers.Scene, .DrawAudio, this );
    		RegisterGui( .Gui, this );			

            local pending_frames = true;

            while ( pending_frames )
            {
                pending_frames = false;

                .audio.ClearInputDataFrame();

                // frequency
                // framerate
                .audio.SetFFTMagnifyScale(.fft_magnify_scale);
                .audio.SetFFTMagnifyPower(.fft_magnify_power);

                .audio.Update();

                if (g_norobot)
                {
                    if (.use_microphone)
                    {
                        .audio.UpdateMicrophoneBuffer();
                        .audio.AddInputDataFrameMicrophone();
                    }

                    if (.use_sine_wave)
                    {
                        .audio.AddInputDataFrameSineWave(.sine_frequency, .sine_amplitude);
                    }
                }
                else
                {
                    .audio.AddInputDataFrameRemoteNao();
                }

                .audio.CalcFrameDFT(0);
                //.audio.CalcFrameFFT(0);
                .audio.CalcFrameAverageAndDifference(0);

                local bps_history_length = 60 * 8;
                local bps = .audio.CalcEstimatedBeatsPerSecond(0, .beat_estimate_bin, .beat_estimate_threshold);

                .beats_per_second_history[.beats_per_second_index] = bps;
                .beats_per_second_index += 1;
                .beats_per_second_index %= bps_history_length;

                local bpm = 0.0f;
                for (i = 0; i < bps_history_length; i += 1)
                {
                    local bpsh = .beats_per_second_history[i];
                    if ( ?bpsh )
                    {
                        bpm += bpsh.Float();
                    }
                }

                if (.show_beat_estimate)
                {
                    print("bps:", bps, "bpm:", bpm / (bps_history_length.Float() / 60.0f));
                }

                if (.tuner_mode)
                {
                    //.audio.NoteTuner(.tuner_threshold);
                    .audio.CalcFramePitches(.tuner_threshold);
                }

                if (.piano_playback_test)
                {
                    local notes = table();

                    .audio.TestGetPianoNotes(.tuner_threshold, notes);

                    local note_map = {
                        "piano_A",
                        "piano_A_sharp",
                        "piano_B",
                        "piano_C",
                        "piano_C_sharp",
                        "piano_D",
                        "piano_D_sharp",
                        "piano_E",
                        "piano_F",
                        "piano_F_sharp",
                        "piano_G",
                        "piano_G_sharp",
                    };

                    foreach ( note in notes )
                    {
                        local key = note % 12;
                        local note_name = note_map[key];

                        if (?.play_thread && threadIsAlive( .play_thread ) )
                        {
                            break;
                        }

                        .play_thread = this:thread( function(note_name) {
                            .piano_soundbank.Play(note_name);
                            sleep(1.0f / 60.0f * 8.0f);
                        }, note_name );
                    }

        			.piano_soundbank.PlayAllQueued();
                }
            }

            yield();
        }
    };

    Main.PlayMelody = function(melody)
    {
        local root = randint(25, 61);
        //local scale = { 0, 2, 4, 5, 7, 9, 11 }; // major
        local scale = { 0, 2, 3, 5, 7, 8, 10 }; // minor
        local beats_per_second = .melody_bpm / 60.0f;
        local measures = .melody_measures;
        local beats_per_measure = .melody_beats;
        local samples_per_second = 44100.0f;
        local samples_per_beat = samples_per_second / beats_per_second;

        this:melody(root, scale, measures, beats_per_measure, samples_per_beat);
    };

    Main.PlayMelody1 = function(root, scale, measures, beats_per_measure, samples_per_beat)
    {
        local count = randint(8, 12);
        local cursor = 0;
        local notes = table();
        local index = randint(0, tableCount(scale));

        for (i = 0; i < count; i += 1)
        {
            if (randint(0, 100) < 20)
            {
                index = max(0, index - 2);
            }

            if (randint(0, 100) < 60)
            {
                index = min(tableCount(scale) - 1, index + 1);
            }

            notes[i] = root + scale[index];
        }

        for (j = 0; j < measures; j += 1)
        {
            for (i = 0; i < beats_per_measure; i += 1)
            {
                if (randint(0, 100) < 10)
                {
                    notes[randint(0, tableCount(notes))] = notes[randint(0, tableCount(notes))];
                }

                local note = notes[i];
                local delay = 0;

                if (randint(0, 100) < 10)
                {
                    delay = samples_per_beat;
                }

                local samples = samples_per_beat * 1.0f;
                local amplitude = randfloat(0.4f, 0.6f);
                
                cursor += delay.Int() + samples.Int();

                local octave = note / 12;
                local note = note % 12;

                .audio.TestAddSynthNote(cursor + delay.Int(), samples.Int(), octave, note, amplitude);
            }
        }
    };

    Main.PlayMelody2 = function(root, scale, measures, beats_per_measure, samples_per_beat)
    {
        local cursor = 0;
        local loop = 4;
        local index = 0;

        for (m = 0; m < measures; m += 1)
        {
            index = 0;

            if (randint(0, 100) < 20) { index += randint(0, 2); }

            for (i = 0; i < beats_per_measure; i += 1)
            {
                local note = root + scale[index];
                
                index += 1;

                if (randint(0, 100) < 10) { index -= 1; }

                local samples = samples_per_beat * 1.0f;
                local amplitude = randfloat(0.4f, 0.6f);

                cursor += samples.Int();
                
                local octave = note / 12;
                local note = note % 12;

                .audio.TestAddSynthNote(cursor, samples.Int(), octave, note, amplitude);
            }
        }
    };

    Main.Init();
    return Main;
};
